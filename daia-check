#!/usr/bin/env perl
use v5.14;
use HTTP::Tiny;
use Time::HiRes qw(usleep);
use Pod::Usage;
use Getopt::Long;
use JSON;

=head1 USAGE

  daia-check [ OPTIONS ] [ ISIL PPN | ID ]

Queries DAIA-Server with given ISIL and PPN or with a full identifier. Checks
whether at least one item is contained in the DAIA response and whether this
item is explicitly marked as available or unavailable. This catches most
possible errors or incomplete DAIA responses.

ISIL/PPN or full identifier can be passed via command line of given in a CSV
file in local file system of at an URL.

=head2 OPTIONS

=over

=item --base

Base URL of DAIA Server. Set to C<http://daia.gbv.de/> by default.

=item --from

Read ISIL and PPN or full IDS from file or URL.

=item --tap

Show all tests in Test Anything Protocol (TAP) format. By default only failing
tests are reported.

=back

=head1 EXAMPLES

   daia-check DE-35 152766766
   daia-check opac-de-35:ppn:152766766
   daia-check --from daia-examples.csv
   daia-check --from http://example.org/daia-examples.csv

=cut

my $errors = 0;
my $tests = 0;
my $tap;
my $base = "http://daia.gbv.de/";
my $from;

GetOptions( 
    "base=s" => \$base, 
    "from=s" => \$from,
    "tap!" => \$tap,
) or pod2usage(2);
 
if ($from) {
    # local CSV file
    if (-f $from) {
        open my $fh, "<", $from or die "failed to open $from\n";
        check_csv(<$fh>);
    } 
    # CSV file from URL
    else {
        if (my $csv = get($from)) {
            check_csv(split "\n", $csv);
        }
    }
} elsif( @ARGV ) {
    check($base, @ARGV);
} else {
    pod2usage(2);
}

say "1..$tests # " . ($errors ? "failed $errors" : "ok");
exit($errors ? 2 : 0);

sub ok {
    my ($ok, $msg) = @_;
    $tests++;

    if (!$ok) {
        say "not ok $tests - $msg";
        $errors++;
    } elsif( $tap ) {
        say "ok $tests - $msg";
    }

    return $ok;
}

sub get {
    my $url = shift;
    my $res = (state $http = HTTP::Tiny->new( 
        agent => 'daia-check',
        default_headers => {
            'Cache-control' => 'no-cache'
        }
    ))->get($url);
    return ok ($res->{success}, $url) ? $res->{content} : undef;
}
 
sub check_csv {
    foreach my $line (@_) {
        state $count = 0;
        next unless $count++; # skip header
        chomp $line;
        my ($isil, $ppn) = split /[ \t,;]+/, $line;        
        check( $base, $isil, $ppn );
        usleep(200000); # wait 0.2 seconds
    }
}

sub check {
    my ($base, $isil, $ppn) = @_;

    # either ISIL and PPN or full id
    my $id = $ppn ? do { $base .= "isil/$isil"; "ppn:$ppn"; } : $isil;

    if (my $res = get("$base?id=$id&format=json")) {
        my $item = eval {
            decode_json($res)->{document}->[0]->{item}->[0];
        } || { };
        ok( ($item->{available} or $item->{unavailable}), 
            ' item available/unavailable');
    }
}
